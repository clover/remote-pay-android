apply from: file("${projectDir}/../versions.gradle")
apply plugin: 'com.android.library'

group = 'com.clover.sdk'
version = "${REMOTE_PAY_ANDROID_VERSION}"

android {
  lintOptions {
    abortOnError false
  }
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 17
    targetSdkVersion 17
  }

  packagingOptions {
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }
}
defaultTasks 'install'

dependencies {
  implementation ("com.clover:clover-remote-interface:${CLOVER_COMMON_VERSION}") {
        transitive = false
  }
  implementation "com.clover.sdk:clover-android-sdk:${CLOVER_SDK_VERSION}"
  implementation "com.clover.sdk:clover-android-loyalty-kit:${CLOVER_SDK_VERSION}"
  implementation "com.google.code.gson:gson:${GSON_VERSION}"
  implementation "com.neovisionaries:nv-websocket-client:${NV_WEBSOCKET_CLIENT_VERSION}"
  testImplementation "junit:junit:${JUNIT_VERSION}"
}

apply plugin: 'signing'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "${upload_release_url}") {
              authentication(userName: "${upload_user}", password: "${upload_password}")
            }
            snapshotRepository(url: "${upload_snapshot_url}") {
              authentication(userName: "${upload_user}", password: "${upload_password}")
            }

            pom.project {
                name 'Clover Connector Android library'
                description 'Clover Connector Android library'
                url 'http://www.clover.com/'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }

                scm {
                    url "https://github.com/clover/remote-pay-android"
                    connection "https://github.com/clover/remote-pay-android.git"
                    developerConnection "git@github.com:clover/remote-pay-android.git"
                }

                developers {
                    developer {
                        name 'Clover Engineering Team'
                        url 'https://www.clover.com/team'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

signing {
    required = { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task javadoc(type: Javadoc) {

    doFirst {
        configurations.implementation
                .filter { it.name.endsWith('.aar') }
        .each { aar ->
            copy {
                from zipTree(aar)
                include "**/classes.jar"
                into "$buildDir/tmp/aarsToJars/${aar.name.replace('.aar', '')}/"
            }
        }
    }

    configurations.implementation.setCanBeResolved(true)
    title = "Android Clover Connector ${REMOTE_PAY_ANDROID_VERSION}"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.implementation
    classpath += fileTree(dir: "$buildDir/tmp/aarsToJars/")
    destinationDir = file("${rootDir}/javadoc/")
    failOnError false
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}


